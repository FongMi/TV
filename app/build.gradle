plugins {
    id 'com.android.application'
}

android {
    compileSdk 33
    flavorDimensions "mode", "api", "abi"

    defaultConfig {
        applicationId "com.fongmi.android.tv"
        minSdk 21
        targetSdk 28
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    productFlavors {
        leanback {
            dimension "mode"
            versionCode 204
            versionName "20230812"
        }
        mobile {
            dimension "mode"
            versionCode 204
            versionName "20230812"
        }
        java {
            dimension "api"
        }
        python {
            dimension "api"
        }
        armeabi_v7a {
            dimension "abi"
            //noinspection ChromeOsAbiSupport
            ndk { abiFilters "armeabi-v7a" }
        }
        arm64_v8a {
            dimension "abi"
            //noinspection ChromeOsAbiSupport
            ndk { abiFilters "arm64-v8a" }
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "${variant.productFlavors[0].name}-${variant.productFlavors[1].name}-${variant.productFlavors[2].name}.apk"
        }
    }

    lint {
        abortOnError false
        disable 'UnsafeOptInUsageError'
    }
}

dependencies {
    implementation project(':hook')
    implementation project(':tvbus')
    implementation project(':catvod')
    implementation project(':quickjs')
    implementation project(':thunder')
    implementation project(':jianpian')
    implementation project(':forcetech')
    implementation project(':ijkplayer')
    //pythonImplementation project(':pyramid')
    implementation 'androidx.media3:media3-database:1.1.0'
    implementation 'androidx.media3:media3-datasource:1.1.0'
    implementation 'androidx.media3:media3-datasource-okhttp:1.1.0'
    implementation 'androidx.media3:media3-datasource-rtmp:1.1.0'
    implementation 'androidx.media3:media3-exoplayer:1.1.0'
    implementation 'androidx.media3:media3-exoplayer-dash:1.1.0'
    implementation 'androidx.media3:media3-exoplayer-hls:1.1.0'
    implementation 'androidx.media3:media3-exoplayer-rtsp:1.1.0'
    implementation 'androidx.media3:media3-exoplayer-smoothstreaming:1.1.0'
    implementation 'androidx.media3:media3-extractor:1.1.0'
    implementation 'androidx.media3:media3-session:1.1.0'
    implementation 'androidx.media3:media3-ui:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.5.1'
    implementation 'androidx.room:room-runtime:2.5.2'
    implementation 'cat.ereza:customactivityoncrash:2.4.0'
    implementation 'com.github.bassaer:materialdesigncolors:1.0.0'
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    implementation 'com.github.bumptech.glide:annotations:4.15.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.15.1'
    implementation 'com.github.TeamNewPipe:NewPipeExtractor:8495ad619e'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'com.google.net.cronet:cronet-okhttp:0.1.0'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.guolindev.permissionx:permissionx:1.7.1'
    implementation 'org.chromium.net:cronet-embedded:101.4951.41'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation('org.simpleframework:simple-xml:2.7.1') { exclude group: 'stax', module: 'stax-api' exclude group: 'xpp3', module: 'xpp3' }
    leanbackImplementation 'androidx.leanback:leanback:1.2.0-alpha02'
    leanbackImplementation 'me.jessyan:autosize:1.2.1'
    mobileImplementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    mobileImplementation 'com.github.chrisbanes:PhotoView:2.3.0'
    mobileImplementation 'com.github.devin1014.DLNA-Cast:dlna-dmc:V1.0.0'
    mobileImplementation('com.journeyapps:zxing-android-embedded:4.3.0') { transitive = false }
    annotationProcessor 'androidx.room:room-compiler:2.5.2'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.15.1'
}